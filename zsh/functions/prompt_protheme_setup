# vi: filetype=zsh

prompt_protheme_help() {
  cat <<EOF
The prompt for proelbtn.
EOF
}

prompt_protheme_async_callback() {
  vcs_info_msg_0_=$3
  zle reset-prompt
}

prompt_protheme_async_precmd() {
  cd -q $1
  vcs_info
  print ${vcs_info_msg_0_}
}

prompt_protheme_precmd() {
  async_flush_jobs vcs_info
  async_job vcs_info prompt_protheme_async_precmd $PWD
}

prompt_protheme_setup_vcs_info() {
  autoload -Uz add_zsh_hook
  autoload -Uz async
  autoload -Uz colors
  autoload -Uz vcs_info

  async_init
  async_start_worker vcs_info
  async_register_callback vcs_info prompt_protheme_async_callback
  add-zsh-hook precmd prompt_protheme_precmd

  zstyle ':vcs_info:*' enable git
}

prompt_protheme_setup() {
  local f_git f_k8s
  local p_date p_plat p_ver p_user p_cwd p_usercwd p_status p_prompt p_vcs

  while [ $# -ne 0 ]; do
    case $1 in
    git)
      f_git=1 
      ;;
    k8s|kubernetes)
      f_k8s=1
      ;;
    esac
    echo $1
    shift
  done

  prompt_opts=(cr percent subst)

  p_date='%F{yellow}[%F{cyan}%D{%Y/%m/%d %R:%S %Z}%f%F{yellow}]%f'
  p_plat="%F{yellow}[%F{cyan}$(uname -m)/$(uname -s)/$(uname -r)%F{yellow}]%f"
  p_ver="%F{yellow}[%F{cyan}${ZSH_VERSION}%F{yellow}]%f"

  if [[ "$(id -un)" = "root" ]]; then
    p_user='%F{red}%n@%m%f'
  elif [[ ${+SSH_CONNECTION} -eq 1 ]]; then
    p_user='%F{blue}%n@%m%f'
  else
    p_user='%F{green}%n@%m%f'
  fi

  p_cwd='%F{yellow}%~%f'
  p_usercwd="%F{yellow}<%f$p_user:$p_cwd%F{yellow}>%f"
  p_status='%(?..%B%F{red}[%?]%f%b )'
  p_prompt='%# '

  p_vcs='%B%U${vcs_info_msg_0_}%u%b'

  PROMPT="$p_date$p_plat$p_ver
$p_usercwd
$p_status$p_prompt"
  RPROMPT="$p_vcs"

  zle_highlight=(default:fg=white, isearch:fg=red,bold,underline)

  prompt_protheme_setup_vcs_info
}

prompt_protheme_setup "$@"
