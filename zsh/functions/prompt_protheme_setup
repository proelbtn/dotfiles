prompt_protheme_help() {
  cat <<'EOF'
This prompt is color-scheme-able.  You can invoke it thus:

  prompt protheme [<color1>]

where the color is for the right-hand prompt.

This prompt was based from walters theme.
EOF
}

prompt_protheme_precmd() {
  vcs_info
}

prompt_protheme_flag() {
  # Privileged
  [[ "${USER}" = "root" ]] && echo -n P || echo -n U

  # Remote connection
  [[ "${SSH_CONNECTION}" != "" ]] && echo -n R || echo -n L
}

prompt_protheme_setup() {
  autoload -Uz ddd_zsh_hook
  autoload -Uz vcs_info

  prompt_opts=(percent subst)

  add-zsh-hook precmd prompt_protheme_precmd

  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' formats "%F{green}[%b]%f"
  zstyle ':vcs_info:*' actionformats '%F{green}[%b|%a]%f'

  PROMPT='%F{yellow}[$(prompt_protheme_flag)]%f %F{cyan}%~%f
%F{red}%(?..[%?] )%f%n@%U%m%u %# '
  RPROMPT='${vcs_info_msg_0_}'
}

prompt_protheme_setup "$@"
