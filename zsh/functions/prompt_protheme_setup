prompt_protheme_help() {
  cat <<'EOF'
This prompt is color-scheme-able.  You can invoke it thus:

  prompt protheme [<color1>]

where the color is for the right-hand prompt.

This prompt was based from walters theme.
EOF
}

prompt_protheme_precmd() {
  vcs_info
}

prompt_protheme_flag() {
  local flag=0

  # Privileged
  [[ "${USER}" = "root" ]] && flag=$((flag + 1))

  # Remote connection
  [[ "${SSH_CONNECTION}" != "" ]] && flag=$((flag + 2))

  case ${flag} in
    0 ) echo "%B%F{green}[UL]%b%f" ;;
    1 ) echo "%B%F{red}[PL]%b%f" ;;
    2 ) echo "%B%F{yellow}[UR]%b%f" ;;
    3 ) echo "%B%F{red}[PR]%b%f" ;;
  esac
}

prompt_protheme_setup() {
  autoload -Uz add_zsh_hook
  autoload -Uz vcs_info

  prompt_opts=(percent subst)

  add-zsh-hook precmd prompt_protheme_precmd

  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' formats "%F{white}[%b]%f"
  zstyle ':vcs_info:*' actionformats '%F{white}[%b|%a]%f'

  PROMPT='$(prompt_protheme_flag) %F{cyan}%~%f
%F{red}%(?..[%?] )%f%n@%U%m%u %# '
  RPROMPT='%f%B%U${vcs_info_msg_0_}%b%u'
}

prompt_protheme_setup "$@"
